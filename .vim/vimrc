""" General
" load filetype settings, plugins and maps
filetype plugin indent on
syntax on

" allow buffers to have changes without being displayed
set hidden
set number relativenumber
set signcolumn=number
set numberwidth=3
set cursorline

set scrolloff=5
set backspace=indent,eol,start  "backspace over anything
set ttimeout ttimeoutlen=30     "timeout on keycodes

set virtualedit=block,onemore   "move past end of line in v-block mode and 1 past end of line in normal mode

" cmd
set showcmd
set cmdheight=2
set shortmess-=S

" reload file when edited outside vim
set autoread

set confirm

" enable project-specific vimrc
set exrc
set secure

""" Filetype
let g:filetype_pl = "prolog"
let g:tex_flavor = "latex"
let g:markdown_fenced_languages = ['python', 'c', 'cpp']

""" OS
if !exists("g:os")
    if has("win64") || has("win32") || has("win16")
        let g:os = "Windows"
    else
        let g:os = substitute(system('uname'), '\n', '', '')
        if stridx(substitute(system('uname'), '\n', '', ''), "MINGW") >= 0
            let g:os = "Windows"
        endif
    endif
endif

""" Plugins
if !has('nvim')
    " Install vim-plug if not found
    let data_dir = has('nvim') ? stdpath('data') . '/site' : '~/.vim'
    if empty(glob(data_dir . '/autoload/plug.vim'))
        silent execute '!curl -fLo '.data_dir.'/autoload/plug.vim --create-dirs  https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
        autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
    endif

    " Run PlugInstall if there are missing plugins
    autocmd VimEnter * if len(filter(values(g:plugs), '!isdirectory(v:val.dir)'))
        \| PlugInstall --sync | source $MYVIMRC
    \| endif

    call plug#begin('~/.vim/vim_plug')

    Plug 'romainl/vim-cool'
    Plug 'romainl/vim-qf'
    Plug 'tpope/vim-abolish'
    "Plug 'ludovicchabant/vim-gutentags'
    "Plug 'arcticicestudio/nord-vim'
    Plug 'sainnhe/everforest'
    Plug 'dense-analysis/ale'

    call plug#end()

    let g:gutentags_ctags_tagfile = '.tags'
    let g:gutentags_project_root = ['.tags']
    let g:gutentags_add_default_project_roots = 0
    let g:gutentags_add_ctrlp_root_markers = 0
    set tags=./.tags;,.tags;
    let g:ale_echo_msg_format = '%linter%: %s'
    let b:ale_python_flake8_options = '--config=$HOME/.config/flake8'
    let b:ale_c_clangd_options="-I " . getcwd()
    let b:ale_linters = ['clangcheck', 'clangd', 'clangtidy', 'cppcheck', 'cpplint']
    let b:ale_linters = ['cabal_ghc', 'hdevtools', 'hie', 'hlint', 'stack_build', 'stack_ghc']
    let b:ale_linters_ignore = ['lacheck']
    let b:ale_cpp_clangd_options="-I " . getcwd()
    set omnifunc=ale#completion#OmniFunc
endif

if exists("$VIRTUAL_ENV")
    let g:python3_host_prog=substitute(system("which -a python3 | head -n2 | tail -n1"), "\n", '', 'g')
else
    let g:python3_host_prog=substitute(system("which python3"), "\n", '', 'g')
endif

""" Wildmenu
set wildmenu                    " Use wildmenu
set wildmode=list:longest,full  " Tab press completes longest common match and lists, second press cycles
set wildignorecase              " Case insensitive, if supported
set wildcharm=<C-z>

""" Indent & Whitespace
set autoindent               " Use indent of previous line on new lines
set smartindent              " Autoindent when starting a new line
set expandtab                " Use spaces instead of tabs
set shiftwidth=4             " Indent with four spaces
set tabstop=4                " Show tab as 4 spaces
set softtabstop=4            " Insert four spaces with tab key
set breakindent              " Indent wrapped lines
"set breakindentopt=shift:2   " Indent wrapped lines slightly more
set showbreak=>\ 
set linebreak                " Break at word boundaries

"set shiftround
"set smarttab

" show trailing spaces
set list
if &encoding == "utf-8"
    set showbreak=↪\ 
    set listchars=tab:»\ ,trail:·,extends:›,precedes:‹,nbsp:␣
else
    set showbreak=\\\ 
    set listchars=tab:>\ ,trail:-,extends:>,precedes:<
endif

""" History, Swap, Undo, Backup
set history=1000

set backup
set undofile

" no undo or backup files for files in /tmp
au BufWritePre /tmp/* setlocal noundofile nobackup nowritebackup

" keep swap, backup and undo files in dedicated directory
" // expands to file's full path
if has('nvim')
    set backupdir-=.
else
    let s:basedir = ""
    if g:os == "Linux"
        let s:basedir = empty($XDG_STATE_HOME) ? "~/.local/state" : $XDG_STATE_HOME
    endif
    if g:os == "Windows"
        let s:basedir = $LOCALAPPDATA
    endif
    if !empty(s:basedir)
        let s:directory = s:basedir."/vim/swap//"
        if !isdirectory(s:directory)
            call mkdir(s:directory, "p", 0700)
        endif
        let &directory=s:directory.",/var/tmp,/tmp"

        let s:directory = s:basedir."/vim/backup//"
        if !isdirectory(s:directory)
            call mkdir(s:directory, "p", 0700)
        endif
        let &backupdir=s:directory

        let s:directory = s:basedir."/vim/undo//"
        if !isdirectory(s:directory)
            call mkdir(s:directory, "p", 0700)
        endif
        let &undodir=s:directory

        let &viminfofile = s:basedir."/vim/viminfo"
    endif
endif

""" Path
" set path in filetype specific settings
set path-=/usr/include

""" Keyword
" treat - as part of words, like _
set iskeyword+=-

""" Completion
" TODO Try out once merged in neovim
"set complete=o,.,w,b,u,t
"set autocomplete

set completeopt=                "control completion menu
set completeopt+=fuzzy          "enable fuzzy-matching
set completeopt+=menuone        "show menu even if there's only 1 match
set completeopt+=noinsert       "don't insert any text before a match was selected
set completeopt+=popup          "show extra info in popup window

set pumheight=7                 "max number of items shown in popup menu

"inoremap <C-Space> <C-x><C-o>
inoremap <expr> <C-Space> pumvisible() ? "\<lt>Down>" : "<C-x><C-o>"

"enter to select highlighted menu item
inoremap <expr> <CR> pumvisible() ? "\<C-y>" : "\<C-g>u\<CR>"

"simulate <Down> to keep a menu item highlighted when using <C-N>
inoremap <expr> <C-n> pumvisible() ? '<C-n>' :
  \ '<C-n><C-r>=pumvisible() ? "\<lt>Down>" : ""<CR>'

"simulate <Up> to keep a menu item highlighted when using <C-P>
inoremap <expr> <C-p> pumvisible() ? '<C-p>' :
  \ '<C-p><C-r>=pumvisible() ? "\<lt>Up>" : ""<CR>'


""" Search
set hlsearch
set ignorecase smartcase
set incsearch

if executable('rg')
    set grepprg=rg\ -S\ --vimgrep
    set grepformat=%f:%l:%c:%m
endif

""" Diff
" uncomment when this is available
" set diffopt+=inline:char
" set diffopt+=anchor

" show diagonal lines in place of deleted lines
set fillchars+=diff:╱

""" Fold
set foldenable
set modeline
set modelineexpr

""" Colorscheme + Font
if !has('nvim')
    set termguicolors
    set background=light
    let g:everforest_background='medium'
    let g:everforest_better_performance=1
    colorscheme everforest
endif
if g:os == "Linux"
    set guifont=Fira_Code_Medium:h11
elseif g:os == "Windows"
    set guifont=Consolas:h11
endif

""" GUI
set guioptions-=m "menu bar
set guioptions-=T "toolbar
set guioptions-=r "scrollbar

""" Statusline
set laststatus=2

""" netrw
let g:netrw_banner = 0
let g:netrw_liststyle = 3
let g:netrw_winsize = 20
let g:netrw_dirhistmax=0

""" Keybindings
let mapleader = "\<Space>"
let maplocalleader = ","
nnoremap <leader><space> <C-^>
nmap <leader><backspace> <Plug>(qf_qf_switch)

nnoremap U <C-R>
nnoremap Y y$
nnoremap Q gQ

" expand %% in command mode to current file path
"cnoremap %% <C-R>=fnameescape(expand('%:h')).'/'<CR>

" search recursively for edit and find
"nnoremap <leader>e :e **/*<C-z>**/*
"nmap <leader>e :e %%<C-z><S-Tab>

nnoremap <localleader>e :e <C-R>=fnameescape(expand('%:h')).'/'<CR><C-z>

" list loaded buffers and populate prompt
nnoremap gb :ls<CR>:b<Space>

nnoremap gd <C-]>
nnoremap gr :execute "silent grep! \\\\b" . expand("<cword>") . "\\\\b" <Bar> cwindow<CR>

" don't add { and } motions to jumplist
nnoremap <silent> } :<C-u>execute "keepjumps norm! " . v:count1 . "}"<CR>
nnoremap <silent> { :<C-u>execute "keepjumps norm! " . v:count1 . "{"<CR>

nnoremap [[ [m
nnoremap ]] ]m
" Cycle through argument list
"nnoremap [a :previous<bar>args<CR><esc>
"nnoremap ]a :next<bar>args<CR><esc>
nnoremap [a <cmd>call <SID>NavArglist(v:count1 * -1)<bar>args<CR><esc>
nnoremap ]a <cmd>call <SID>NavArglist(v:count1)<bar>args<CR><esc>
" Cycle through buffers
nnoremap [b :bprevious<CR>
nnoremap ]b :bnext<CR>
" Cycle through quicklist/:helpgrep items
"nnoremap [q :cprevious<CR>
"nnoremap ]q :cnext<CR>
nmap [q <Plug>(qf_qf_previous)
nmap ]q  <Plug>(qf_qf_next)
" Cycle through location list items
"nnoremap [l :lprevious<CR>
"nnoremap ]l :lnext<CR>
nmap [l <Plug>(qf_loc_previous)
nmap ]l  <Plug>(qf_loc_next)
" Cycle through tags
nnoremap [t :tprevious<CR>
nnoremap ]t :tnext<CR>
" Go to previous/next conflict marker
nnoremap <silent> [x :<C-U>call <SID>Context(1)<CR>
nnoremap <silent> ]x :<C-U>call <SID>Context(0)<CR>

" Add current file to arglist
nnoremap <leader>aa <cmd>$arge %<bar>argded<bar>args<cr>
" Remove current file from arglist
nnoremap <leader>ad <cmd>argd %<bar>args<cr>
" Clear arglist
nnoremap <leader>ac <cmd>%argd<cr><C-L>
" Navigate to current file in arglist
nnoremap <leader>a<Space> <cmd>argu<bar>args<cr>

function! s:NavArglist(count)
    let arglen = argc()
    if arglen == 0
        return
    endif
    let next = fmod(argidx() + a:count, arglen)
    if next < 0
        let next += arglen
    endif
    exe float2nr(next + 1) .. 'argu'
endfunction

function! s:Context(reverse) abort
  call search('^\(@@ .* @@\|[<=>|]\{7}[<=>|]\@!\)', a:reverse ? 'bW' : 'W')
endfunction

nnoremap <leader>y :%y+<CR>

"" vim:fdm=expr:fdl=0
"" vim:fde=getline(v\:lnum)=~'^""'?'>'.(matchend(getline(v\:lnum),'""*')-2)\:'='
